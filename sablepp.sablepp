Namespace SablePP.Compiler;

Helpers
    all = [0 .. 0xFFFF];
    lowercase = ['a' .. 'z'];
    uppercase = ['A' .. 'Z'];
    digit = ['0' .. '9'];
    hex_digit = [digit + [['a' .. 'f'] + ['A' .. 'F']]];

    tab = 9;
    cr = 13;
    lf = 10;
    eol = cr lf | cr | lf;

    not_cr_lf = [all - [cr + lf]];
    not_star = [all - '*'];
    not_star_slash = [not_star - '/'];

    blank = (' ' | tab | eol)+;

    short_comment = '//' not_cr_lf*;
    long_comment = '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/';
    comment = short_comment | long_comment;

    letter = lowercase | uppercase | '_' | '$';
    id_part = lowercase (lowercase | digit)*;

States
    normal,
    package,
    highlight,
    highlightstyles;

Tokens
{package}
    packagename = letter (letter | digit)* ('.' letter (letter | digit)*)*;

{normal->package,highlight->package}
    packagetoken = 'Package'|'Namespace';
{normal,highlight->normal}
    statestoken = 'States';
{normal,highlight->normal}
    helperstoken = 'Helpers';
{normal,highlight->normal}
    tokenstoken = 'Tokens';
{normal,highlight->normal}
    ignoredtoken = 'Ignored';
{normal,highlight->normal}
    productionstoken = 'Productions';
{normal,highlight->normal}
    asttoken = 'Abstract' (' ' | tab)+ 'Syntax' (' ' | tab)+ 'Tree';
{normal->highlight}
    highlighttoken = 'Token Syntax Highlight';
    
    new = 'New';
    null = 'Null';

    token_specifier = 'T';
    production_specifier = 'P';

    dot = '.';
    d_dot = '..';

{normal, package->normal, highlightstyles->highlight}
    semicolon = ';';

    equal = '=';
    l_bkt = '[';
    r_bkt = ']';
    l_par = '(';
    r_par = ')';
    l_brace =  '{';
{normal, highlight->highlightstyles}
    r_brace =  '}';
    plus = '+';
    minus = '-';
    q_mark = '?';
    star = '*';
    pipe = '|';
    comma = ',';
    slash = '/';
    arrow = '->';
    colon = ':';

{normal, highlight}
    identifier = id_part ('_' id_part)*;

    character = ''' not_cr_lf ''';
{normal, highlightstyles}
    dec_char = digit+;
    hex_char = '0' ('x' | 'X') hex_digit+;

    string = ''' [not_cr_lf - ''']+ ''';

    blank = blank;
    comment = comment;
    
{highlightstyles}
    italic = 'italic';
{highlightstyles}
    bold = 'bold';
{highlightstyles}
    text = 'text';
{highlightstyles}
    background = 'background';
{highlightstyles}
    rgb = 'rgb';
{highlightstyles}
    hsv = 'hsv'|'hsb';
{highlightstyles}
    hex_color = '#' hex_digit+;

Ignored Tokens
    blank,
    comment;

Productions
    grammar = section* { -> New grammar.section([section])};
    section
        = {p} package { -> New section.package(package) }
        | {h} helpers { -> New section.helpers(helpers) }
        | {s} states { -> New section.states(states) }
        | {t} tokens { -> New section.tokens(tokens) }
        | {i} ign_tokens { -> New section.ignore(ign_tokens) }
        | {pr} productions { -> New section.productions(productions) }
        | {a} astproductions { -> New section.a_s_t(astproductions) }
        | {hi} highlightrules { -> New section.highlight(highlightrules) }
        ;

    package =
        packagetoken packagename semicolon;

    helpers
        = helperstoken [helper_defs]:helper_def+ {-> New helpers(helperstoken, [helper_defs])}
        ;

    helper_def {-> helper}
        = identifier equal reg_exp semicolon {-> New helper(identifier, equal, reg_exp, semicolon)}
        ;

    states
        = statestoken s_list semicolon {-> New states(statestoken, [s_list], semicolon)}
        ;
    s_list -> state+
    = identifier s_list_tail* -> [New state(identifier), s_list_tail]
        ;
    s_list_tail -> state
        = comma identifier -> New state(identifier)
        ;

    id_list {-> identifier+}
        = identifier [ids]:id_list_tail* {-> [identifier, ids]}
        ;

    id_list_tail {-> identifier}
        = comma identifier {-> identifier}
        ;

    tokens
        = tokenstoken [token_defs]:token_def+ {-> New tokens(tokenstoken, [token_defs])}
        ;

    token_def {-> token}
        = {none} identifier equal reg_exp look_ahead? semicolon {-> New token([], identifier, equal, reg_exp, look_ahead, semicolon)}
        | state_list identifier equal reg_exp look_ahead? semicolon {-> New token([state_list], identifier, equal, reg_exp, look_ahead, semicolon)}
        ;

    state_list {-> token_state+}
        = {notransition} l_brace identifier [state_lists]:state_list_tail* r_brace
          {-> [New token_state(identifier), state_lists]}
        | {transition} l_brace [f]:identifier arrow [t]:identifier [state_lists]:state_list_tail* r_brace
          {-> [New token_state.transition(f, arrow, t), state_lists]}
        ;

    state_list_tail {-> token_state}
        = {notransition} comma identifier {-> New token_state(identifier)}
        | {transition} comma [f]:identifier arrow [t]:identifier {-> New token_state.transition(f, arrow, t)}
        ;

    ign_tokens {-> ignoredtokens}
        = ignoredtoken tokenstoken id_list semicolon {-> New ignoredtokens(ignoredtoken, tokenstoken, [id_list], semicolon)}
        ;

    look_ahead {-> tokenlookahead}
        = slash reg_exp {-> New tokenlookahead(slash, reg_exp)}
        ;

    reg_exp {-> regex}
        = {single} concat {-> concat}
        | {multiple} concat [concats]:reg_exp_tail+ {-> New regex.or([concat, concats])}
        ;

    reg_exp_tail {-> regex}
        = pipe concat {-> concat}
        ;

    concat {-> regex}
        = [un_exps]:un_exp+ {-> New regex.concatenated([un_exps])}
        ;
    
    modifier
        = {star} star
        | {question} q_mark
        | {plus} plus
        ;
    
    un_exp {-> regex}
        = {simple} basic {-> basic}
        | {mod} basic modifier {-> New regex.unary(basic, modifier)}
        ;

    basic {-> regex}
        = {char} char {-> char}
        | {set} set {-> set}
        | {string} string {-> New regex.string(string)}
        | {identifier} identifier {-> New regex.identifier(identifier)}
        | {reg_exp} l_par reg_exp r_par {-> New regex.parenthesis(l_par, reg_exp, r_par)}
        ;

    char {-> regex}
        = {char} character {-> New regex.char(character)}
        | {dec}  dec_char {-> New regex.dec(dec_char)}
        | {hex}  hex_char {-> New regex.hex(hex_char)}
        ;

    set {-> regex}
        = {plus} l_bkt [left]:basic plus [right]:basic r_bkt {-> New regex.binaryplus(l_bkt, left, plus, right, r_bkt)}
        | {minus} l_bkt [left]:basic minus [right]:basic r_bkt {-> New regex.binaryminus(l_bkt, left, minus, right, r_bkt)}
        | {interval} l_bkt [left]:char d_dot [right]:char r_bkt {-> New regex.interval(l_bkt, left, d_dot, right, r_bkt)}
        ;

    productions
        = productionstoken [prods]:prod+ {-> New productions(productionstoken, [prods])}
        ;

    prod {-> production}
        = identifier prodtrans? equal alts semicolon {-> New production(identifier, prodtrans, equal, [alts], semicolon)}
        ;
    
    prodtrans {-> prodtranslation}
        = l_brace arrow identifier modifier? r_brace {-> New prodtranslation(arrow, identifier, modifier)}
        | {nomod} arrow identifier modifier? {-> New prodtranslation(arrow, identifier, modifier)}
        ;
    
    alttrans {-> translation}
        = {braces} l_brace arrow translation r_brace {-> New translation.full(arrow, translation)}
        | arrow translation {-> New translation.full(arrow, translation)}
        ;
    translation
        = {id} translationid {-> translationid}
        | {new} new identifier l_par translation_args? r_par {-> New translation.new(new, identifier, l_par, [translation_args], r_par)}
        | {newalt} new [a]:identifier dot [b]:identifier l_par translation_args? r_par {-> New translation.newalternative(new, a, dot, b, l_par, [translation_args], r_par)}
        | {null} null {-> New translation.null(null)}
        | {list} l_bkt translation_args? r_bkt {-> New translation.list(l_bkt, [translation_args], r_bkt)}
        ;
    translation_args {-> translation+}
        = translation translation_args_tail* {-> [translation, translation_args_tail]}
        ;
    translation_args_tail {-> translation}
        = comma translation {-> translation}
        ;
    translationid {-> translation}
        = {single} identifier {-> New translation.id(identifier)}
        | {double} [a]:identifier dot [b]:identifier {-> New translation.iddotid(a,dot,b)}
        ;

    alts {-> alternative+}
        = {simple} alt_name? [elems]:elem* alttrans? {-> [New alternative(alt_name, [elems], alttrans)]}
        | {multiple} alt_name? [elems]:elem* alttrans? [alts]:alts_tail+ {-> [New alternative(alt_name, [elems], alttrans), alts]}
        ;

    alts_tail {-> alternative}
        = pipe alt_name? [elems]:elem* alttrans? {-> New alternative(alt_name, [elems], alttrans)}
        ;
        
    alt_name {-> alternativename}
        = l_brace identifier r_brace {-> New alternativename(l_brace, identifier, r_brace)}
        ;

    elem {-> element} = elem_name? elem_id modifier? {-> New element(elem_name, elem_id, modifier)};

    elem_name {-> elementname}
        = l_bkt identifier r_bkt colon {-> New elementname(l_bkt, identifier, r_bkt, colon)}
        ;
    
    elem_id {-> elementid}
        = {clean} identifier {-> New elementid.clean(identifier)}
        | {token} token_specifier dot identifier {-> New elementid.token(token_specifier, dot, identifier)}
        | {production} production_specifier dot identifier {-> New elementid.production(production_specifier, dot, identifier)}
        ;

    astproductions
        = asttoken [prods]:prod+ {-> New astproductions(asttoken, [prods])}
        ;
    
    highlightrules
        = highlighttoken highlightrule+
        ;
    
    highlightrule
        = l_brace id_list r_brace hlstyle_list semicolon {-> New highlightrule(l_brace, [id_list], r_brace, [hlstyle_list], semicolon)}
        ;
    
    hlstyle_list {-> highlight_style+ }
    = hlstyle hlstyle_tail* {-> [hlstyle, hlstyle_tail]}
        ;
    
    hlstyle_tail {-> highlight_style }
        = comma hlstyle {-> hlstyle}
        ;
        
    hlstyle {-> highlight_style}
        = {italic} italic {-> New highlight_style.italic(italic)}
        | {bold} bold {-> New highlight_style.bold(bold)}
        | {text} text colon color {-> New highlight_style.text(text, colon, color) }
        | {background} background colon color {-> New highlight_style.background(background, colon, color) }
        ;
    
    color
        = {rgb} rgb l_par [red]:dec_char [comma1]:comma [green]:dec_char [comma2]:comma [blue]:dec_char r_par
        | {hsv} hsv l_par [hue]:dec_char [comma1]:comma [sat]:dec_char [comma2]:comma [bri]:dec_char r_par
        | {hex} hex_color
        ;

Abstract Syntax Tree
    grammar
        = package? helpers? states? tokens? ignoredtokens? productions? astproductions? highlightrules?
        | {section} section*
        ;
    section
        = {package} package
        | {helpers} helpers
        | {states} states
        | {tokens} tokens
        | {ignore} ignoredtokens
        | {productions} productions
        | {a_s_t} astproductions
        | {highlight} highlightrules
        ;
    
    package = packagetoken packagename semicolon;
    
    helpers = helperstoken [helpers]:helper*;
    helper = identifier equal regex semicolon;
    
    tokens = tokenstoken [tokens]:token*;
    token = [statelist]:token_state* identifier equal regex tokenlookahead? semicolon;
    tokenlookahead = slash regex;
    
    regex
        = {char} character
        | {dec} dec_char
        | {hex} hex_char
        
        | {concatenated} regex+
        | {unary} regex modifier
        
        | {binaryplus} [lpar]:l_bkt [left]:regex plus [right]:regex [rpar]:r_bkt
        | {binaryminus} [lpar]:l_bkt [left]:regex minus [right]:regex [rpar]:r_bkt
        | {interval} [lpar]:l_bkt [left]:regex [dots]:d_dot [right]:regex [rpar]:r_bkt
        
        | {string} string
        | {identifier} identifier
        | {parenthesis} [lpar]:l_par regex [rpar]:r_par
        
        | {or} regex+
        ;
    
    modifier
        = {star} star
        | {question} q_mark
        | {plus} plus
        ;
    
    states = statestoken state+ semicolon;
    state = identifier;
    ignoredtokens = ignoredtoken tokenstoken [tokens]:identifier+ semicolon;
    
    token_state
        = identifier
        | {transition} [from]:identifier arrow [to]:identifier
        ;

    productions = productionstoken [productions]:production+;
    astproductions =      asttoken [productions]:production+;
    production = identifier prodtranslation? equal alternative+ semicolon;
    
    prodtranslation = arrow identifier modifier?;
    
    translation
        = {full} arrow translation
        | {new} new [production]:identifier [lpar]:l_par [arguments]:translation* [rpar]:r_par
        | {newalternative} new [production]:identifier dot [alternative]:identifier [lpar]:l_par [arguments]:translation* [rpar]:r_par
        | {list} [lpar]:l_bkt [elements]:translation* [rpar]:r_bkt
        | {null} null
        | {id} identifier
        | {iddotid} identifier dot [production]:identifier
        ;
    
    alternative = alternativename? element* translation?;
    
    alternativename = [lpar]:l_brace [name]:identifier [rpar]:r_brace;
    
    element = elementname? elementid modifier?;
    elementname = [lpar]:l_bkt [name]:identifier [rpar]:r_bkt colon;
    elementid
        = {clean} identifier
        | {token} token_specifier dot identifier
        | {production} production_specifier dot identifier
        ;
    
    highlightrules
        = highlighttoken highlightrule+
        ;
    highlightrule
        = [lpar]:l_brace [tokens]:identifier+ [rpar]:r_brace [styles]:highlight_style+ semicolon
        ;
    highlight_style
        = {italic} italic
        | {bold} bold
        | {text} text colon color
        | {background} background colon color
        ;
    
    color
        = {rgb} rgb l_par [red]:dec_char [comma1]:comma [green]:dec_char [comma2]:comma [blue]:dec_char r_par
        | {hsv} hsv l_par [hue]:dec_char [comma1]:comma [saturation]:dec_char [comma2]:comma [brightness]:dec_char r_par
        | {hex} [color]:hex_color
        ;
        
Token Syntax Highlight
    comment {comment} text:#008000;
    string {character, string} text:rgb(163, 21, 21);
    heading {packagename, packagetoken, helperstoken, tokenstoken, ignoredtoken, statestoken, productionstoken, asttoken, highlighttoken} text:hsv(0, 0, 30), bold;
    bold {bold} bold;
    italic {italic} italic;
    